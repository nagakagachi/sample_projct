#if 0

assp_probe_spatial_filter_cs.hlsl

AdaptiveScreenSpaceProbe OctMap(4x4) に対するシンプルな edge-aware 空間フィルタ。
近傍は上下左右のみ参照し、法線向きで棄却、深度差で重み付けしてブレンドする。

#endif

#include "assp_probe_common.hlsli"

RWTexture2D<float4> RWAdaptiveScreenSpaceProbeFilteredTex;

[numthreads(ADAPTIVE_SCREEN_SPACE_PROBE_OCT_RESOLUTION, ADAPTIVE_SCREEN_SPACE_PROBE_OCT_RESOLUTION, 1)]
void main_cs(
    uint3 dtid : SV_DispatchThreadID,
    uint3 gtid : SV_GroupThreadID,
    uint3 gid : SV_GroupID,
    uint gindex : SV_GroupIndex)
{
    uint2 probe_tex_size;
    AdaptiveScreenSpaceProbeTex.GetDimensions(probe_tex_size.x, probe_tex_size.y);
    if (any(dtid.xy >= probe_tex_size))
    {
        return;
    }

    uint2 tile_info_size;
    AdaptiveScreenSpaceProbeTileInfoTex.GetDimensions(tile_info_size.x, tile_info_size.y);

    const int2 probe_tile_id = int2(gid.xy);
    if (any(probe_tile_id >= int2(tile_info_size)))
    {
        return;
    }

    const int2 local_cell = int2(gtid.xy);
    const int2 center_texel_pos = probe_tile_id * ADAPTIVE_SCREEN_SPACE_PROBE_OCT_RESOLUTION + local_cell;

    const float4 center_value = AdaptiveScreenSpaceProbeTex.Load(int3(center_texel_pos, 0));
    const float4 center_tile_info = AdaptiveScreenSpaceProbeTileInfoTex.Load(int3(probe_tile_id, 0));
    if (!isValidDepth(center_tile_info.x))
    {
        RWAdaptiveScreenSpaceProbeFilteredTex[center_texel_pos] = center_value;
        return;
    }

    const float center_depth = center_tile_info.x;
    const float3 center_normal = normalize(OctDecode(center_tile_info.zw));

    float4 accum_value = center_value;
    float accum_weight = 1.0;

    const int2 neighbor_offsets[4] =
    {
        int2(-1, 0),
        int2(1, 0),
        int2(0, -1),
        int2(0, 1)
    };

    [unroll]
    for (int i = 0; i < 4; ++i)
    {
        int2 neighbor_tile = int2(-1, -1);
        if(!AsspTryResolveSpatialFilterNeighborRepresentativeTileId(probe_tile_id, neighbor_offsets[i], neighbor_tile))
        {
            continue;
        }

        const float4 neighbor_tile_info = AdaptiveScreenSpaceProbeTileInfoTex.Load(int3(neighbor_tile, 0));
        if (!isValidDepth(neighbor_tile_info.x))
        {
            continue;
        }

        const float3 neighbor_normal = OctDecode(neighbor_tile_info.zw);
        const float normal_dot = dot(center_normal, neighbor_normal);
        if (normal_dot < cb_instant_rdv.assp_spatial_filter_normal_cos_threshold)
        {
            continue;
        }

        const float depth_diff = abs(neighbor_tile_info.x - center_depth) / max(center_depth, 1e-4);
        const float depth_weight = exp(-cb_instant_rdv.assp_spatial_filter_depth_exp_scale * depth_diff);
        const float4 neighbor_value = AdaptiveScreenSpaceProbeTex.Load(int3(neighbor_tile * ADAPTIVE_SCREEN_SPACE_PROBE_OCT_RESOLUTION + local_cell, 0));

        accum_value += neighbor_value * depth_weight;
        accum_weight += depth_weight;
    }

    RWAdaptiveScreenSpaceProbeFilteredTex[center_texel_pos] = (accum_weight > 0.0) ? (accum_value / accum_weight) : center_value;
}
