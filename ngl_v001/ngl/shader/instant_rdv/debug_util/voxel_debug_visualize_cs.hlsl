
#if 0

ss_voxel_debug_visualize_cs.hlsl

デバッグ可視化.

#endif


#include "../instant_rdv_util.hlsli"
#include "../assp/assp_probe_common.hlsli"

// SceneView定数バッファ構造定義.
#include "../../include/scene_view_struct.hlsli"

ConstantBuffer<SceneViewInfo> cb_ngl_sceneview;

RWTexture2D<float4>	RWTexWork;
SamplerState		SmpLinearClamp;

float debug_count_to_rate(float count)
{
    return count / (count + 4.0);
}

// デバッグテクスチャに対してDispatch.
[numthreads(16, 16, 1)]
void main_cs(
	uint3 dtid	: SV_DispatchThreadID,
	uint3 gtid : SV_GroupThreadID,
	uint3 gid : SV_GroupID,
	uint gindex : SV_GroupIndex
)
{
    uint2 work_tex_size_u;
    RWTexWork.GetDimensions(work_tex_size_u.x, work_tex_size_u.y);
	const float2 screen_pos_f = float2(dtid.xy) + float2(0.5, 0.5);// ピクセル中心への半ピクセルオフセット考慮.
	const float2 work_tex_size_f = float2(work_tex_size_u);
	const float2 screen_size_f = float2(cb_instant_rdv.tex_main_view_depth_size.xy);
	const float2 screen_uv = (screen_pos_f / work_tex_size_f);
    const int2 texel_pos = clamp(int2(screen_uv * screen_size_f), int2(0, 0), int2(cb_instant_rdv.tex_main_view_depth_size.xy) - 1);
    
	const float3 camera_dir = GetViewDirFromInverseViewMatrix(cb_ngl_sceneview.cb_view_inv_mtx);
	const float3 view_origin = GetViewOriginFromInverseViewMatrix(cb_ngl_sceneview.cb_view_inv_mtx);
    
    const float3 to_pixel_ray_vs = CalcViewSpaceRay(screen_uv, cb_ngl_sceneview.cb_proj_mtx);
    const float3 ray_dir_ws = mul(cb_ngl_sceneview.cb_view_inv_mtx, float4(to_pixel_ray_vs, 0.0));

    const int debug_category = cb_instant_rdv.debug_view_category;
    const int debug_sub_mode = cb_instant_rdv.debug_view_sub_mode;

    // Category 0: BBV.
    if(0 == debug_category)
    {
        if((0 == debug_sub_mode) || (2 <= debug_sub_mode && 5 >= debug_sub_mode))
        {
            // Voxel単位Traceのテスト.
            const float trace_distance = 10000.0;          
            int hit_voxel_index = -1;
            float4 debug_ray_info;
            float4 curr_ray_t_ws = trace_bbv_dev(
                hit_voxel_index, debug_ray_info,
                view_origin, ray_dir_ws, trace_distance, 
                cb_instant_rdv.bbv.grid_min_pos, cb_instant_rdv.bbv.cell_size, cb_instant_rdv.bbv.grid_resolution,
                cb_instant_rdv.bbv.grid_toroidal_offset, BitmaskBrickVoxel, false);

            float4 debug_color = float4(0, 0, 1, 0);
            if(0.0 <= curr_ray_t_ws.x)
            {
                const float fog_rate0 = pow(saturate((curr_ray_t_ws.x - 20.0)/100.0), 1.0/1.2);
                const float fog_rate1 = saturate((curr_ray_t_ws.x - 70.0)/500.0);

                const uint brick_occupied_voxel_count = BitmaskBrickVoxel[bbv_voxel_coarse_occupancy_info_addr(hit_voxel_index)];
                // デバッグ用テクスチャにモード別描画.
                if(0 == debug_sub_mode)
                {
                    // bbvセル可視化
                    const float3 bbv_cell_id = floor((view_origin + ray_dir_ws*(curr_ray_t_ws.x + 0.001)) * (cb_instant_rdv.bbv.cell_size_inv*float(k_bbv_per_voxel_resolution)));
                    debug_color.xyz = float4(noise_float_to_float(bbv_cell_id.xyzz), noise_float_to_float(bbv_cell_id.xzyy), noise_float_to_float(bbv_cell_id.xyzx), 1);

                    // 簡易フォグ.
                    debug_color.xyz = lerp(debug_color.xyz, float3(1,1,1), fog_rate0 * 0.8);
                    debug_color.xyz = lerp(debug_color.xyz, float3(0.1,0.1,1), fog_rate1 * 0.8);
                }
                else if(2 == debug_sub_mode)
                {
                    // VoxelIDを可視化.
                    debug_color.xyz = float4(noise_float_to_float(hit_voxel_index), noise_float_to_float(hit_voxel_index*2), noise_float_to_float(hit_voxel_index*3), 1);
                    
                    // 簡易フォグ.
                    debug_color.xyz = lerp(debug_color.xyz, float3(1,1,1), fog_rate0 * 0.8);
                    debug_color.xyz = lerp(debug_color.xyz, float3(0.1,0.1,1), fog_rate1 * 0.8);
                }
                else if(3 == debug_sub_mode)
                {
                    // Bbvセルのヒット法線可視化.
                    const float3 bbv_cell_id = floor((view_origin + ray_dir_ws*(curr_ray_t_ws.x + 0.001)) * (cb_instant_rdv.bbv.cell_size_inv*float(k_bbv_per_voxel_resolution)));
                    debug_color.xyz = abs(curr_ray_t_ws.yzw);
                    
                    // 簡易フォグ.
                    debug_color.xyz = lerp(debug_color.xyz, float3(1,1,1), fog_rate0 * 0.8);
                    debug_color.xyz = lerp(debug_color.xyz, float3(0.1,0.1,1), fog_rate1 * 0.8);
                }
                else if(4 == debug_sub_mode)
                {
                    // Bbvセルの深度を可視化.
                    debug_color.xyz = float4(saturate(curr_ray_t_ws.x/100.0), saturate(curr_ray_t_ws.x/100.0), saturate(curr_ray_t_ws.x/100.0), 1);
                }
                else if(5 == debug_sub_mode)
                {
                    const float count_rate = saturate(float(brick_occupied_voxel_count) / float(k_bbv_per_voxel_bitmask_bit_count));
                    debug_color.xyz = lerp(float3(0.0, 0.0, 0.1), float3(1.0, 0.8, 0.2), count_rate);
                }
            }
            RWTexWork[dtid.xy] = debug_color;
        }
        else if(1 == debug_sub_mode)
        {
            // 最細セル単位の色分けを標準トレーサで可視化して比較するモード.
            const float trace_distance = 10000.0;
            int hit_voxel_index = -1;
            float4 debug_ray_info;
            float4 curr_ray_t_ws = trace_bbv_dev(
                hit_voxel_index, debug_ray_info,
                view_origin, ray_dir_ws, trace_distance,
                cb_instant_rdv.bbv.grid_min_pos, cb_instant_rdv.bbv.cell_size, cb_instant_rdv.bbv.grid_resolution,
                cb_instant_rdv.bbv.grid_toroidal_offset, BitmaskBrickVoxel, false);

            float4 debug_color = float4(0, 0, 1, 0);
            if(0.0 <= curr_ray_t_ws.x)
            {
                const float3 bbv_cell_id = floor((view_origin + ray_dir_ws*(curr_ray_t_ws.x + 0.001)) * (cb_instant_rdv.bbv.cell_size_inv*float(k_bbv_per_voxel_resolution)));
                debug_color.xyz = float4(noise_float_to_float(bbv_cell_id.xyzz), noise_float_to_float(bbv_cell_id.xzyy), noise_float_to_float(bbv_cell_id.xyzx), 1);
                debug_color.xyz = lerp(debug_color.xyz, float3(1.0, 0.35, 0.15), 0.18);

                // 簡易フォグ.
                debug_color.xyz = lerp(debug_color.xyz, float3(1,1,1), pow(saturate((curr_ray_t_ws.x - 20.0)/100.0), 1.0/1.2) * 0.8);
                debug_color.xyz = lerp(debug_color.xyz, float3(0.1,0.1,1), saturate((curr_ray_t_ws.x - 70.0)/500.0) * 0.8);
            }
            RWTexWork[dtid.xy] = debug_color;
        }
        else if(6 == debug_sub_mode)
        {
            // Brick単位Traceのテスト. Brickの占有フラグが適切に設定または除去されているかのテスト.
            const float trace_distance = 10000.0;          
            int hit_voxel_index = -1;
            float4 debug_ray_info;
            float4 curr_ray_t_ws = trace_bbv_dev(
                hit_voxel_index, debug_ray_info,
                view_origin, ray_dir_ws, trace_distance, 
                cb_instant_rdv.bbv.grid_min_pos, cb_instant_rdv.bbv.cell_size, cb_instant_rdv.bbv.grid_resolution,
                cb_instant_rdv.bbv.grid_toroidal_offset, BitmaskBrickVoxel, true);
                
            float4 debug_color = float4(0, 0, 1, 0);
            if(0.0 <= curr_ray_t_ws.x)
            {
                // VoxelIDを可視化.
                debug_color.xyz = float4(noise_float_to_float(hit_voxel_index), noise_float_to_float(hit_voxel_index*2), noise_float_to_float(hit_voxel_index*3), 1);
                
                // 簡易フォグ.
                debug_color.xyz = lerp(debug_color.xyz, float3(1,1,1), pow(saturate((curr_ray_t_ws.x - 20.0)/100.0), 1.0/1.2) * 0.8);
                debug_color.xyz = lerp(debug_color.xyz, float3(0.1,0.1,1), saturate((curr_ray_t_ws.x - 70.0)/500.0) * 0.8);
            }
            RWTexWork[dtid.xy] = debug_color;
        }
        else if(7 == debug_sub_mode)
        {
            // Voxel上面図X-Ray表示.
            const int3 bv_full_reso = cb_instant_rdv.bbv.grid_resolution * k_bbv_per_voxel_resolution;
            const float visualize_scale = 0.5;
            float3 read_pos_world_base = (float3(dtid.x, 0.0, cb_instant_rdv.tex_main_view_depth_size.y-1 - dtid.y) + 0.5) * visualize_scale * cb_instant_rdv.bbv.cell_size/k_bbv_per_voxel_resolution;
            read_pos_world_base += cb_instant_rdv.bbv.grid_min_pos;

            float write_data = 0.0;
            for(int yi = 0; yi < bv_full_reso.y; ++yi)
            {
                const float3 read_pos_world = read_pos_world_base + float3(0.0, yi, 0.0) * (cb_instant_rdv.bbv.cell_size/k_bbv_per_voxel_resolution);

                const uint bit_value = read_bbv_voxel_from_world_pos(BitmaskBrickVoxel, cb_instant_rdv.bbv.grid_resolution, cb_instant_rdv.bbv.grid_toroidal_offset, cb_instant_rdv.bbv.grid_min_pos, cb_instant_rdv.bbv.cell_size_inv, read_pos_world);

                float occupancy = float(bit_value);
                occupancy /= (float)bv_full_reso.y;

                write_data += occupancy * 8.0;
            }

            RWTexWork[dtid.xy] = float4(write_data, write_data, write_data, 1.0);
        }
        else if(8 == debug_sub_mode)
        {
            // Brick coarse check count.
            const float trace_distance = 10000.0;
            int hit_voxel_index = -1;
            float4 debug_ray_info;
            trace_bbv_dev(
                hit_voxel_index, debug_ray_info,
                view_origin, ray_dir_ws, trace_distance,
                cb_instant_rdv.bbv.grid_min_pos, cb_instant_rdv.bbv.cell_size, cb_instant_rdv.bbv.grid_resolution,
                cb_instant_rdv.bbv.grid_toroidal_offset, BitmaskBrickVoxel, false);

            const float brick_check_rate = debug_count_to_rate(debug_ray_info.z);
            RWTexWork[dtid.xy] = float4(lerp(float3(0.02, 0.02, 0.02), float3(0.1, 1.0, 0.2), brick_check_rate), 1.0);
        }
        else if(9 == debug_sub_mode)
        {
            // fine voxel / bitmask check count.
            const float trace_distance = 10000.0;
            int hit_voxel_index = -1;
            float4 debug_ray_info;
            trace_bbv_dev(
                hit_voxel_index, debug_ray_info,
                view_origin, ray_dir_ws, trace_distance,
                cb_instant_rdv.bbv.grid_min_pos, cb_instant_rdv.bbv.cell_size, cb_instant_rdv.bbv.grid_resolution,
                cb_instant_rdv.bbv.grid_toroidal_offset, BitmaskBrickVoxel, false);

            const float bitmask_check_rate = debug_count_to_rate(debug_ray_info.w);
            RWTexWork[dtid.xy] = float4(lerp(float3(0.02, 0.02, 0.02), float3(1.0, 0.7, 0.1), bitmask_check_rate), 1.0);
        }
        else if(10 == debug_sub_mode)
        {
            // Brick coarse check count.
            const float trace_distance = 10000.0;
            int hit_voxel_index = -1;
            float4 debug_ray_info;
            trace_bbv_dev(
                hit_voxel_index, debug_ray_info,
                view_origin, ray_dir_ws, trace_distance,
                cb_instant_rdv.bbv.grid_min_pos, cb_instant_rdv.bbv.cell_size, cb_instant_rdv.bbv.grid_resolution,
                cb_instant_rdv.bbv.grid_toroidal_offset, BitmaskBrickVoxel, false);

            const float brick_check_rate = debug_count_to_rate(debug_ray_info.z);
            RWTexWork[dtid.xy] = float4(lerp(float3(0.02, 0.02, 0.02), float3(0.1, 0.7, 1.0), brick_check_rate), 1.0);
        }
        else if(11 == debug_sub_mode)
        {
            // fine voxel / bitmask check count.
            const float trace_distance = 10000.0;
            int hit_voxel_index = -1;
            float4 debug_ray_info;
            trace_bbv_dev(
                hit_voxel_index, debug_ray_info,
                view_origin, ray_dir_ws, trace_distance,
                cb_instant_rdv.bbv.grid_min_pos, cb_instant_rdv.bbv.cell_size, cb_instant_rdv.bbv.grid_resolution,
                cb_instant_rdv.bbv.grid_toroidal_offset, BitmaskBrickVoxel, false);

            const float bitmask_check_rate = debug_count_to_rate(debug_ray_info.w);
            RWTexWork[dtid.xy] = float4(lerp(float3(0.02, 0.02, 0.02), float3(1.0, 0.2, 0.8), bitmask_check_rate), 1.0);
        }
        else if(12 == debug_sub_mode)
        {
            // detail efficiency = bitmask_check / max(brick_check, 1).
            const float trace_distance = 10000.0;
            int hit_voxel_index = -1;
            float4 debug_ray_info;
            trace_bbv_dev(
                hit_voxel_index, debug_ray_info,
                view_origin, ray_dir_ws, trace_distance,
                cb_instant_rdv.bbv.grid_min_pos, cb_instant_rdv.bbv.cell_size, cb_instant_rdv.bbv.grid_resolution,
                cb_instant_rdv.bbv.grid_toroidal_offset, BitmaskBrickVoxel, false);

            const float detail_efficiency = debug_ray_info.w / max(debug_ray_info.z, 1.0);
            RWTexWork[dtid.xy] = float4(lerp(float3(0.8, 0.1, 0.1), float3(0.1, 1.0, 0.2), detail_efficiency), 1.0);
        }
        else if(13 == debug_sub_mode)
        {
            // Brick occupancy による簡易 debug 可視化。
            const float trace_distance = 10000.0;
            int hit_voxel_index = -1;
            float4 debug_ray_info;
            const float4 curr_ray_t_ws = trace_bbv_dev(
                hit_voxel_index, debug_ray_info,
                view_origin, ray_dir_ws, trace_distance,
                cb_instant_rdv.bbv.grid_min_pos, cb_instant_rdv.bbv.cell_size, cb_instant_rdv.bbv.grid_resolution,
                cb_instant_rdv.bbv.grid_toroidal_offset, BitmaskBrickVoxel, false);

            float average_brick_occupancy = 0.0;
            if(0.0 <= curr_ray_t_ws.x)
            {
                const uint brick_occupied_voxel_count = BitmaskBrickVoxel[bbv_voxel_coarse_occupancy_info_addr(hit_voxel_index)];
                average_brick_occupancy = saturate(float(brick_occupied_voxel_count) / float(k_bbv_per_voxel_bitmask_bit_count));
            }
            const float traced_brick_rate = debug_count_to_rate(debug_ray_info.z);

            float3 debug_color = lerp(float3(0.02, 0.03, 0.05), float3(1.0, 0.75, 0.15), average_brick_occupancy);
            debug_color = lerp(debug_color, float3(0.15, 0.85, 1.0), average_brick_occupancy * 0.6);
            debug_color = lerp(debug_color, float3(0.2, 1.0, 0.3), traced_brick_rate * 0.3);
            RWTexWork[dtid.xy] = float4(debug_color, 1.0);
        }
        else if(14 == debug_sub_mode)
        {
            // Resolve 済み Brick radiance 可視化.
            const float trace_distance = 10000.0;
            int hit_voxel_index = -1;
            float4 debug_ray_info;
            float4 curr_ray_t_ws = trace_bbv_dev(
                hit_voxel_index, debug_ray_info,
                view_origin, ray_dir_ws, trace_distance,
                cb_instant_rdv.bbv.grid_min_pos, cb_instant_rdv.bbv.cell_size, cb_instant_rdv.bbv.grid_resolution,
                cb_instant_rdv.bbv.grid_toroidal_offset, BitmaskBrickVoxel, false);

            float3 debug_color = float3(0.0, 0.0, 0.0);
            if(0.0 <= curr_ray_t_ws.x)
            {
                const BbvOptionalData voxel_optional_data = BitmaskBrickVoxelOptionData[hit_voxel_index];
                debug_color = voxel_optional_data.resolved_radiance / (1.0 + voxel_optional_data.resolved_radiance);
                debug_color = pow(max(debug_color, 0.0.xxx), 1.0 / 2.2);
            }
            RWTexWork[dtid.xy] = float4(debug_color, 1.0);
        }
    }
    // Category 1: FSP.
    else if(1 == debug_category)
    {
        if(0 == debug_sub_mode)
        {
            // FSP OctahedralMap atlas raw RGBA.
            const int2 texel_pos = dtid.xy * 0.1;
            uint tex_width, tex_height;
            FspProbeAtlasTex.GetDimensions(tex_width, tex_height);
            if(any(int2(tex_width, tex_height) <= texel_pos))
                return;

            RWTexWork[dtid.xy] = FspProbeAtlasTex.Load(uint3(texel_pos, 0));
        }
        else if(1 == debug_sub_mode)
        {
            // Dense IrradianceVolume SH をX-major cell index順に2Dへ展開して表示する。
            const uint irradiance_volume_cell_index =
                dtid.x + dtid.y * uint(cb_instant_rdv.fsp_cascade[0].grid.flatten_2d_width);
            if(irradiance_volume_cell_index >= (uint)cb_instant_rdv.fsp_total_cell_count)
            {
                return;
            }

            // FSP IrradianceVolume SH texture raw RGBA.
            RWTexWork[dtid.xy] = FspIrradianceVolumeLoadCoeff(irradiance_volume_cell_index, 0);
        }
    }
    // Category 2: ASSP.
    else if(2 == debug_category)
    {
        const int2 representative_tile_id = texel_pos / ADAPTIVE_SCREEN_SPACE_PROBE_INFO_DOWNSCALE;
        uint2 tile_info_size_u32;
        AdaptiveScreenSpaceProbeTileInfoTex.GetDimensions(tile_info_size_u32.x, tile_info_size_u32.y);
        const bool is_in_range = all(representative_tile_id >= int2(0, 0)) && all(representative_tile_id < int2(tile_info_size_u32));
        const float4 representative_tile_info = is_in_range ? AdaptiveScreenSpaceProbeTileInfoTex.Load(int3(representative_tile_id, 0)) : float4(1.0, 0.0, 0.0, 0.0);
        const bool is_valid_rep = is_in_range && isValidDepth(representative_tile_info.x);

        if(!is_valid_rep)
        {
            RWTexWork[dtid.xy] = float4(0.02, 0.02, 0.02, 1.0);
        }
        else
        {
            if(0 == debug_sub_mode)
            {
                RWTexWork[dtid.xy] = AdaptiveScreenSpaceProbeTex.Load(int3(texel_pos, 0));
            }
            else if(1 == debug_sub_mode)
            {
                uint2 packed_sh_tex_size;
                AdaptiveScreenSpaceProbePackedSHTex.GetDimensions(packed_sh_tex_size.x, packed_sh_tex_size.y);
                const int2 packed_sh_texel_pos = texel_pos / 2;
                if(any(packed_sh_texel_pos >= int2(packed_sh_tex_size)))
                {
                    RWTexWork[dtid.xy] = float4(0.02, 0.02, 0.02, 1.0);
                }
                else
                {
                    RWTexWork[dtid.xy] = AdaptiveScreenSpaceProbePackedSHTex.Load(int3(packed_sh_texel_pos, 0));
                }
            }
            else if(2 == debug_sub_mode)
            {
                const float4 sh_basis = EvaluateL1ShBasis(normalize(-cb_instant_rdv.main_light_dir_ws));
                const float4 coeff0 = AsspPackedShAtlasLoadCoeff(representative_tile_id, 0);
                const float4 coeff1 = AsspPackedShAtlasLoadCoeff(representative_tile_id, 1);
                const float4 coeff2 = AsspPackedShAtlasLoadCoeff(representative_tile_id, 2);
                const float4 coeff3 = AsspPackedShAtlasLoadCoeff(representative_tile_id, 3);
                const float3 radiance = max(float3(
                    dot(float4(coeff0.g, coeff1.g, coeff2.g, coeff3.g), sh_basis),
                    dot(float4(coeff0.b, coeff1.b, coeff2.b, coeff3.b), sh_basis),
                    dot(float4(coeff0.a, coeff1.a, coeff2.a, coeff3.a), sh_basis)), 0.0.xxx);
                RWTexWork[dtid.xy] = float4(radiance / (1.0 + radiance), 1.0);
            }
            else if(3 == debug_sub_mode)
            {
                const float4 variance_signal = AdaptiveScreenSpaceProbeVarianceTex.Load(int3(representative_tile_id, 0));
                const float filtered_mean = max(variance_signal.x, 0.0);
                const float mean_vis = filtered_mean / (1.0 + filtered_mean);
                RWTexWork[dtid.xy] = float4(mean_vis, mean_vis, mean_vis, 1.0);
            }
            else if(4 == debug_sub_mode)
            {
                const float4 variance_signal = AdaptiveScreenSpaceProbeVarianceTex.Load(int3(representative_tile_id, 0));
                const float filtered_second_moment = max(variance_signal.y, 0.0);
                const float filtered_mean = max(variance_signal.x, 0.0);
                const float filtered_variance = max(filtered_second_moment - filtered_mean * filtered_mean, 0.0);
                const float variance_vis = filtered_variance / (0.1 + filtered_variance);
                const float3 debug_color = lerp(float3(0.02, 0.02, 0.05), float3(1.0, 0.35, 0.1), variance_vis);
                RWTexWork[dtid.xy] = float4(debug_color, 1.0);
            }
            else if(5 == debug_sub_mode)
            {
                const float4 variance_signal = AdaptiveScreenSpaceProbeVarianceTex.Load(int3(representative_tile_id, 0));
                const float raw_mean = max(variance_signal.z, 0.0);
                const float mean_vis = raw_mean / (1.0 + raw_mean);
                RWTexWork[dtid.xy] = float4(mean_vis, mean_vis, mean_vis, 1.0);
            }
            else if(6 == debug_sub_mode)
            {
                const float4 variance_signal = AdaptiveScreenSpaceProbeVarianceTex.Load(int3(representative_tile_id, 0));
                const float raw_variance = max(variance_signal.w, 0.0);
                const float variance_vis = raw_variance / (0.1 + raw_variance);
                const float3 debug_color = lerp(float3(0.02, 0.02, 0.05), float3(1.0, 0.35, 0.1), variance_vis);
                RWTexWork[dtid.xy] = float4(debug_color, 1.0);
            }
            else if(7 == debug_sub_mode)
            {
                uint probe_linear_index = 0u;
                if(!AsspTryGetProbeLinearIndexFromTileId(representative_tile_id, probe_linear_index))
                {
                    RWTexWork[dtid.xy] = float4(0.02, 0.02, 0.02, 1.0);
                }
                else
                {
                    const uint packed_meta = AsspProbeRayMetaBuffer[probe_linear_index];
                    const uint ray_count_u = AsspUnpackRayMetaCount(packed_meta);
                    const float t = saturate(float(ray_count_u) / float(k_assp_ray_count_max));
                    RWTexWork[dtid.xy] = float4(t, t, t, 1.0);
                }
            }
            else
            {
                RWTexWork[dtid.xy] = float4(0.02, 0.02, 0.02, 1.0);
            }
        }
    }
}
